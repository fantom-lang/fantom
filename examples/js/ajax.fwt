//
// Copyright (c) 2009, Brian Frank and Andy Frank
// Licensed under the Academic Free License version 3.0
//
// History:
//   9 Oct 09  Andy Frank  Creation
//

using dom
using gfx
using fwt

class Ajax : Window
{
  new make() : super(null, null)
  {
    get  := Text { prefCols=50 }
    post := Text { prefCols=50 }
    form := Text { prefCols=50 }
    content = BorderPane
    {
      insets = Insets(24)
      GridPane
      {
        numCols = 2
        Button {
          text = "Get!"
          onAction.add {
            HttpReq { uri=`/echo` }.get |res| {
              get.text = res.content
            }
          }
        }, get,
        Button {
          text = "Post!"
          onAction.add {
            HttpReq { uri=`/echo` }.post("This is a request from the server!") |res| {
              post.text = res.content
            }
          }
        }, post,
        Button {
          text = "Form!"
          onAction.add {
            HttpReq { uri=`/echo` }.postForm(["foo":"bar"]) |res| {
              form.text = res.content
            }
          }
        }, form,
      },
    }
  }
}

