//
// Copyright (c) 2011, Brian Frank and Andy Frank
// Licensed under the Academic Free License version 3.0
//
// History:
//   07 Mar 11  Yuri Strot  Creation
//

using gfx
using fwt

@Js class Cursors : Window
{
  new make() : super(null, null)
  {
    content = InsetPane(12)
    {
      GridPane
      {
        vgap = 24
        GridPane
        {
          numCols = 3
          grid := it
          Cursor.predefined.each |Cursor c|
          {
            grid.add(CursorDemo(c))
          }
          c := Image(`fan://icons/x16/refresh.png`)
          grid.add(CursorDemo(Cursor(c, 8, 8)))
        },
        GridPane
        {
          hgap = 12
          numCols = 4
          Button { text="Default" },
          Button { text="Crosshair"; it.cursor=Cursor.crosshair },
          Label { text="Default" },
          Label { text="Crosshair"; it.cursor=Cursor.crosshair },
        },
        GridPane
        {
          hgap = 12
          numCols = 2
          BorderPane
          {
            border = Border("#080")
            insets = Insets(24)
            it.cursor = Cursor.wait
            Label { bg=Color("#cfc"); text="Inherit | Wait" },
          },
          BorderPane
          {
            border = Border("#800")
            insets = Insets(24)
            it.cursor = Cursor.wait
            Label { bg=Color("#fcc"); text="Override | Help"; it.cursor=Cursor.help },
          },
        },
      },
    }
  }

  Void main() { open }
}

@Js class CursorDemo : Canvas
{
  new make(Cursor c)
  {
    this.text = c.toStr
    this.cursor = c
    d := |e| { dump(e) }
    onMouseEnter.add(d)
    onMouseExit.add(d)
    onMouseMove.add(d)
  }

  override Size prefSize(Hints hints := Hints.defVal) { return Size.make(150, 30) }

  override Void onPaint(Graphics g)
  {
    w := size.w; h := size.h
    font := Desktop.sysFont
    g.brush = Color.white
    g.fillRect(0, 0, w - 1, h - 1)
    g.brush = Color.black
    g.drawRect(0, 0, w - 1, h - 1)
    g.font = font
    g.drawText(text, (w - font.width(text)) / 2, (h - font.height()) / 2)
    if (p != null)
    {
      g.brush = Color.red
      g.drawLine(p.x - 10, p.y - 10, p.x + 10, p.y + 10)
      g.drawLine(p.x - 10, p.y + 10, p.x + 10, p.y - 10)
    }
  }

  Void dump(Event event)
  {
    p = event.id != EventId.mouseExit ? event.pos : null
    repaint
  }

  const Str text
  Point? p
}